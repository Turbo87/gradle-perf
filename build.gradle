buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:0.5'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group = 'gradleperf'
version = '1.1.0'

dependencies {
    compile gradleApi()
    compile localGroovy()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    group = 'build'
    description = 'Assembles a jar archive containing the main sources.'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    group = 'build'
    description = 'Assembles a jar archive containing the javadocs.'
    classifier = 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.getProperty('bintrayUsername') : ''
    key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : ''
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = 'gradle-perf'
    }
}
